Integer Tokens
Integer Blocks
Integer StackPieces
Double TokenHeight
Double BlockHeight
Double Nozzle_Height_Compensation
Double Nozzle_Tray_Height_Compensation
Integer i

Integer TokenID
Integer BlockID

Real timeTask1 'Time elapsed for task1
Real timeTask2 'Time elapsed for task2


Function main

Motor On
Power Low
Speed 15
Accel 15, 15
SpeedS 1500
AccelS 2200, 2200
Tool 1

TokenHeight = 6.0
BlockHeight = 6.0
Nozzle_Height_Compensation = 0.0		'Change to define picking height
Nozzle_Tray_Height_Compensation = 0.0	'Change to define dropping height to the tray

Pallet 1, P14, P17, P16, 2, 3		'Tray pallet


	'Z axis is inverted on all locals because we found that it is geometrically
	'more convenient to define locals that way
	
	' *Green Button lets all program run
	
Go Retract_Safe
	
Move Workablehight
	
Do		'Button press loop

	'---- Task 1: Pick & Place ----'

    If Sw(1) = On Then	'If White Button is pressed, Task 1 will be executed
		
		Tokens = 3				'number of tokens to place
    	Blocks = 3				'number of blocks to place

		TmReset 1				'Timer starts for Task1
		Print "Task 1 Timer Started"
		
		
    	For TokenID = Tokens To 1 Step -1	'Token Loop
        	Pick_Infeed_Token(TokenID)
        	Alignment_Token()
        	Place_Tray_Token(TokenID)
    	Next TokenID

    	Go Retract_Safe

    	For BlockID = Blocks To 1 Step -1	'Block Loop
       		Pick_Infeed_Block(BlockID)
        	Alignment_Block()
        	Place_Tray_Block(BlockID)
    	Next BlockID

    	Go Retract_Safe
    	
    	Go Home2
    	
    	timeTask1 = Tmr(1)		'Timer ends for Task1
    	Print "Task 1 Completed. Time:" + Str$(timeTask1) + " seconds"
    	
    
     EndIf

	
	
	'---- Task 2: Stacking ----' 	

    If Sw(2) = On Then	'If Blue Button is pressed, Task 2 will be executed

		Tokens = 10				'number of tokens to stack
    	Blocks = 10				'number of blocks to stack

		StackPieces = Tokens + Blocks	'number of pieces to be stacked

		TmReset 2				'Timer starts for Task2
		Print "Task 2 Timer Started"
		
		TokenID = Tokens
		BlockID = Blocks
						
		'Task 2 functions go here
			For i = 1 To StackPieces			'Stacking loop
			
				If (i Mod 2) = 0 Then		'Pick and align block
					Pick_Infeed_Token(TokenID)
					TokenID = TokenID - 1
				Else						'Pick and align token
				   	Pick_Infeed_Block(BlockID)
				   	BlockID = BlockID - 1
				EndIf

				Call Place_Stack(i)				'Place piece being held
			Next i

    	Go Retract_Safe
    	Go Home2

    	timeTask2 = Tmr(2)		'Timer ends for Task2
    	Print "Task 2 Completed. Time:" + Str$(timeTask2) + " seconds"
    
    EndIf

Loop



Fend




'===========================================
'          Shared Functions
'===========================================

Function Pick_Infeed_Token(Tokens_Left As Integer)		'Pick token

    Go Feeder_Token -Z(50 + (Tokens_Left * TokenHeight))

    Integer Infeed_Height
    Infeed_Height = Tokens_Left * TokenHeight
	Print "Infeed token height: ", Infeed_Height

    Go Feeder_Token -Z((Tokens_Left * TokenHeight) + Nozzle_Height_Compensation)
    On 8						'Vacuum nozzle on
    Wait .5
    Go Feeder_Token +X(-2) -Z(50 + (Tokens_Left * TokenHeight))



Fend

Function Pick_Infeed_Block(Blocks_Left As Integer)		'Pick block

    Go Feeder_Block -Z(50 + (Blocks_Left * BlockHeight))

    Integer Infeed_Height
    Infeed_Height = Blocks_Left * BlockHeight
	Print "Infeed token height: ", Infeed_Height

    Go Feeder_Block -Z((Blocks_Left * BlockHeight) + Nozzle_Height_Compensation)
    On 8
    Wait .5
    Go Feeder_Block +X(-2) +Y(-2) -Z(50 + (Blocks_Left * BlockHeight))


Fend

Function Alignment_Token      'Aligns Tokens using inclined surface
	Go Workablehight 		'Back to work position

    Go Drop_Align -Z(40)       'Approching security position
    Go Drop_Align '-Z(10)       'Approach near ramp

    Off 8                         'Turn off vacuum nozzle
    Wait 0.8                      'Accomodation time

    Go Align_Token -Z(Nozzle_Height_Compensation)        'Approach token
    On 8                          'Vacuum nozzle on 
    Wait 0.3

    Go Align_Token -Z(40)

	Go Workablehight 		'Back to work position

Fend

Function Alignment_Block       'Aligns Blocks using inclined surface
	Go Workablehight 		'Back to work position

    Go Drop_Align -Z(40) 		'Approching security position
    Go Drop_Align '-Z(10)		'Approach near ramp

    Off 8						'Turn off vacuum nozzle
    Wait 0.8					'Accomodation time

    Go Align_Block -Z(Nozzle_Height_Compensation)		'Approach block
    On 8						'Vacuum nozzle on
    Wait 0.3

    Go Align_Block -Z(40)

	Go Workablehight 		'Back to work position

Fend


Function Place_Tray_Token(Tokens_Left As Integer)	'Place Token in tray

    Go Pallet(1, 2, Tokens_Left) -Z(20)
    Go Pallet(1, 2, Tokens_Left) -Z(Nozzle_Tray_Height_Compensation)
    Off 8
    Wait .5
    Go Pallet(1, 2, Tokens_Left) -Z(20)


	Go Workablehight 		'Back to work position
	
Fend

Function Place_Tray_Block(Blocks_Left As Integer)	'Place Block in tray

    Go Pallet(1, 1, Blocks_Left) -Z(20)
    Go Pallet(1, 1, Blocks_Left) -Z(Nozzle_Tray_Height_Compensation)
    Off 8
    Wait .5
    Go Pallet(1, 2, Blocks_Left) -Z(20)


	Go Workablehight 		'Back to work position
	
Fend


Function Place_Stack(Index As Integer)		'Stack pieces
	  Go Pile -Z((Index * TokenHeight) + 12)
	  Go Pile -Z((Index * TokenHeight) - 3 + Nozzle_Height_Compensation)
	
	  Off 8       ' VACUUM OFF
	  Wait 0.3
	
	  Go Pile -Z((Index * TokenHeight) + 12)
	
Fend
